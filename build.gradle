plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.24.0'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me" }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    // Cloth config
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}"
    // Mod Menu
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    // TOML4J
    include(implementation("io.hotmoka:toml4j:${project.toml4j_version}"))
}

processResources {
    inputs.properties([
            "version" : project.version,
            "mcversion" : project.minecraft_version,
            "fabricloaderversion" : project.loader_version,
            "modmenuversion" : project.modmenu_version,
            "clothconfigversion" : project.clothconfig_version
    ])
    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "mcversion": project.minecraft_version,
                "fabricloaderversion": project.loader_version,
                "modmenuversion": project.modmenu_version,
                "clothconfigversion": project.clothconfig_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
    }
}

spotless {
    java {
        eclipse()
        licenseHeader(
            "/*\n" +
            " * Copyright Kaylen Dart 2022\n" +
            " * This project is licensed under the GNU GPLv3 license. See the LICENSE file for more information.\n" +
            " */"
        )
    }
}
